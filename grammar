Gramatica independiente del contexto - mPascal
===============================================

programa : funcion
         | programa funcion

funcion : FUN ID argumento locales BEGIN lineas END

argumento : PARI PARD
          | PARI declaracion PARD

declaracion : dec
            | declaracion COMA dec

locales : loclist PCOMA
        | vacio

loclist : loc
        | loclist PCOMA loc

loc : dec
    | funcion

dec : ID DPUN type

lineas : linea 
       | lineas PCOMA linea

linea : expre
      | WRITE PARI expre PARD
      | READ PARI location PARD
      | PRINT PARI STRING PARD  
      | ID ASIG expre
      | RETURN expre
      | call
      | SKIP
      | BREAK
      | WHILE relacion DO linea
      | IF relacion THEN linea else
      | BEGIN lineas END

call : ID PARI exprelist PARD

else : ELSE linea
     | vacio

location : ID
         | ID CORI expre CORD

relacion : expre LT expre
         | expre LE expre
         | expre GT expre
         | expre GE expre
         | expre EQ expre
         | expre NE expre
         | relacion AND relacion
         | relacion OR relacion
         | NOT relacion
         | PARI relacion PARD

type  : FLOAT
      | INT
      | FLOAT CORI expre CORD
      | INT CORI expre CORD

exprelist :  exprelist COMA expre
          |  expre

expre   : expre  MAS expre 
        | expre  MENOS expre
        | expre  MUL expre 
        | expre  DIV expre  
        | MENOS expre 
        | MAS   expre   
        | call
        | ID
        | ID CORI expre CORD
        | INUM
        | FNUM
        | INT PARI expre PARD
        | FLOAR PARI expre PARD 